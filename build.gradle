buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2' }
    }

    dependencies {
        classpath 'gradle.plugin.net.minecrell:licenser:0.4.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.1'
    }
}

defaultTasks 'licenseFormat', 'build'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = 'me.lucko.luckperms'
    version = '5.2-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    jar {
        from '../LICENSE.txt'
    }

    def determinePatchVersion = {
        // get the name of the last tag
        def tagInfo = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = tagInfo
        }
        tagInfo = tagInfo.toString()

        if (!tagInfo.contains('-')) {
            return 0
        }
        return tagInfo.split("-")[1]
    }

    project.ext.majorVersion = '5'
    project.ext.minorVersion = '2'
    project.ext.patchVersion = determinePatchVersion()
    project.ext.apiVersion = project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.fullVersion = project.ext.apiVersion + '.' + project.ext.patchVersion

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
        newLine = true
    }

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.lucko.me/' }
        maven { url 'https://maven.aura-dev.team/repository/auradev-releases/' }
    }
}

dependencies {
    compile project(':bukkit')
    compile project(':bungee')
    compile project(':sponge')
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'pluginVersion': project.ext.fullVersion
        include 'plugin.yml'
    }
}

shadowJar {
    archiveName = "LuckPerms-Universal-${project.ext.fullVersion}.jar"

    dependencies {
        include(dependency('net.luckperms:.*'))
        include(dependency('me.lucko.luckperms:.*'))
        include(dependency('team.aura_dev.lib.multiplatformcore:MultiPlatformCore'))
    }

    relocate 'team.aura_dev.lib.multiplatformcore', 'me.lucko.luckperms.lib.multiplatformcore'
}

artifacts {
    archives shadowJar
}
